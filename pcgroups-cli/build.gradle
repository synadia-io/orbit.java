plugins {
    id("java")
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Apply the shadow plugin
}

version = "0.1.0"
def originalShadow = 'pcg-cli-' + version + '-all.jar'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots" }
}

dependencies {
    implementation 'io.nats:jnats:2.25.1'
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'io.synadia:pcgroups:0.1.0'
    implementation 'info.picocli:picocli:4.7.5'

    testImplementation 'io.nats:jnats-server-runner:3.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.14.3'
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'io.synadia.pcg.cli.CgCommand'
    }
}

tasks.register('package', Copy) {
    dependsOn shadowJar

    // we want the file to be called cg.jar
    from ('build/libs')
    include originalShadow
    destinationDir file('build/')
    rename originalShadow, "cg.jar"
}

tasks.register('createZip', Zip) {
    dependsOn 'package'

    archiveFileName = "cg.zip"
    destinationDirectory = file('build/')
    from (files('build/cg.jar'))
}

tasks.register('createTar', Tar) {
    dependsOn 'package'

    archiveFileName = "cg.tar"
    destinationDirectory = file('build/')
    from (files('build/cg.jar'))
}

tasks.register('dist') {
    dependsOn createZip
    dependsOn createTar
}
