plugins {
    id("java")
    id("java-library")
    id("maven-publish")
    id("jacoco")
    id("biz.aQute.bnd.builder") version "7.1.0"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

group = 'io.synadia'
version = "0.1.0"
def originalUber = 'pcg-cli-' + version + '-uber.jar'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots" }
}

dependencies {
    implementation 'io.nats:jnats:2.25.1'
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'io.synadia:pcgroups:0.1.0-SNAPSHOT'
    implementation 'info.picocli:picocli:4.7.5'

    testImplementation 'io.nats:jnats-server-runner:3.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.14.3'
}

tasks.register('copyToLib', Copy) {
    into "build/libs"
    from configurations.runtimeClasspath
}

tasks.register('packageUberJar', Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn copyToLib
}

tasks.register('uberJar', Copy) {
    dependsOn packageUberJar

    // we want the file to be called cg.jar
    from ('build/libs')
    include originalUber
    destinationDir file('build/libs/')
    rename originalUber, "cg.jar"

    // this task actually does a copy, so this part removes the original
    doLast {
        delete('build/libs/' + originalUber)
    }
}
