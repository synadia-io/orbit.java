import aQute.bnd.gradle.Bundle

plugins {
    id("java")
    id("java-library")
    id("maven-publish")
    id("jacoco")
    id("biz.aQute.bnd.builder") version "7.1.0"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

def jarVersion = "0.2.1"
group = 'io.synadia'

def isRelease = System.getenv("BUILD_EVENT") == "release"

def tc = System.getenv("TARGET_COMPATIBILITY");
def targetCompat = tc == "21" ? JavaVersion.VERSION_21 : (tc == "17" ? JavaVersion.VERSION_17 : JavaVersion.VERSION_1_8)
def jarEnd =       tc == "21" ?  "-jdk21"              : (tc == "17" ? "-jdk17"               : "")
def jarAndArtifactName = "counters" + jarEnd

version = isRelease ? jarVersion : jarVersion + "-SNAPSHOT" // version is the variable the build actually uses.


System.out.println("Java: " + System.getProperty("java.version"))
System.out.println("Target Compatibility: " + targetCompat)
System.out.println(group + ":" + jarAndArtifactName + ":" + version)

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = targetCompat
}

repositories {
    mavenCentral()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots/" }
}

dependencies {
    implementation 'io.nats:jnats:2.24.0'
    implementation 'io.synadia:direct-batch:0.1.4'
    implementation 'org.jspecify:jspecify:1.0.0'

    testImplementation 'io.nats:jnats-server-runner:1.2.8'

    testImplementation 'commons-codec:commons-codec:1.20.0'
    testImplementation 'com.github.stefanbirkner:system-lambda:1.2.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:4.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','src/examples/java']
        }
        resources {
            srcDirs = ['src/examples/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.register('bundle', Bundle) {
    from sourceSets.main.output
    exclude("io/synadia/examples/**")
}

jar {
    bundle {
        bnd("Bundle-Name": "io.synadia.counters",
                "Bundle-Vendor": "synadia.io",
                "Bundle-Description": "JetStream Distributed Counters",
                "Bundle-DocURL": "https://github.com/synadia-io/orbit.java/tree/main/counters",
                "Target-Compatibility": "Java " + targetCompat
        )
    }
}

test {
    useJUnitPlatform()
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "Synadia Communications Inc. JetStream Distributed Counters"
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('examplesJar', Jar) {
    archiveClassifier.set('examples')
    manifest {
        attributes('Implementation-Title': 'JetStream Distributed Counters',
                'Implementation-Version': jarVersion,
                'Implementation-Vendor': 'synadia.io')
    }
    from(sourceSets.main.output) {
        include "io/synadia/examples/**"
    }
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, examplesJar
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
    afterEvaluate { // only report on main library not examples
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/examples**'])
        }))
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact examplesJar
            artifact javadocJar
            pom {
                name = jarAndArtifactName
                packaging = 'jar'
                groupId = group
                artifactId = jarAndArtifactName
                description = 'Synadia Communications Inc. JetStream Distributed Counters'
                url = 'https://github.com/synadia-io/orbit.java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "synadia"
                        name = "Synadia"
                        email = "info@synadia.com"
                        url = "https://synadia.io"
                    }
                }
                scm {
                    url = 'https://github.com/synadia-io/orbit.java'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}
